#!/usr/bin/perl -w

use strict;

use FindBin;
use Getopt::Long qw( :config pass_through require_order );

sub usage {
    my($status, @msg) = @_;
    my $fh = $status == 0 ? \*STDOUT : \*STDERR;
    print $fh @msg, "\n" if @msg;
    print $fh <<EOF;
Usage:
  rlwrap-jdb [OPT]... [RLWRAP-OPT]... PROG [ARG]...
Run PROG with command-line editing and history as well as handy aliases,
command repetition, and breakpoint completion for jdb.  Note PROG doesn't
have to be jdb; it could be an application startup script, a mvn exec:exec
invocation, etc. that eventually calls jdb.

Options:
  --breakpoints-file F  File containing breakpoints for completion;
                        generate with list-java-breakpoints.
  --main-class C        The main class to debug.  This is not essential, and
                        rlwrap-jdb can often figure it out itself.

RLWRAP-OPTs must be specified ofter OPTs, and are passed to rlwrap.  Default
rlwrap options:
  --command-name jdb
  --break-chars :
  --histsize 5000
  --wait-before-prompt -40
EOF
    exit($status);
}

my($breakpoints_file, $main_class);
GetOptions(
    help                 => sub { usage(0) },
    'breakpoints-file=s' => \$breakpoints_file,
    'main-class=s'       => \$main_class,
) or usage(1);

usage(1, 'PROG required') if @ARGV == 0;

exec(
    qw( rlwrap
        --command-name jdb --break-chars : --histsize 5000
        --wait-before-prompt -40
        --filter ),
    join(' ',
        "$FindBin::Bin/rlwrap-jdb-filter",
        defined($breakpoints_file)
            ? ('--breakpoints-file', $breakpoints_file) : (),
        defined($main_class)
            ? ('--main-class', $main_class) : (),
    ),
    @ARGV)
    or die $!;
